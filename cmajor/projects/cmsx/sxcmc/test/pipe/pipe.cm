using System;
using System.Text;
using System.Os;

int main()
{
    try
    {
        int readFD = 0;
        int writeFD = 0;
        Pipe(&readFD, &writeFD);
        int pid1 = Fork();
        if (pid1 == 0)
        {
            Close(1);
            Dup(writeFD);
            Close(writeFD);
            Close(readFD);
            for (int i = 0; i < 100; ++i)
            {
                string line;
                line.Append(Format(ToString(i), 3, FormatWidth.exact, FormatJustify.right, '0'));
                line.Append(' ');
                line.Append("A bird in the hand is worth two in the bush.");
                Console.Out() << line << endl();
            }
            return 0;
        }
        int pid2 = Fork();
        if (pid2 == 0)
        {
            Close(0);
            Dup(readFD);
            Close(readFD);
            Close(writeFD);
            while (!Console.In().Eof())
            {
                string line = Console.ReadLine();
                Console.WriteLine(line);
            }
            return 0;
        }
        Close(readFD);
        Close(writeFD);
        int pid = wait(null);
        while (pid != -1)
        {
            pid = wait(null);
        }
        Console.Out() << "that's all" << endl();
    }
    catch (const Exception& ex)
    {
        Console.Error() << ex.Message() << endl();
    }
    return 0;
}
