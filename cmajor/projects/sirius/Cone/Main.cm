// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Sirius.Graphics;
using Sirius.Math;

class MainWindow : System.Windows.Window
{
    public nothrow MainWindow() :
        base("Cone"),
        whitePen(System.Windows.Color.White()),
        lightSource(Sirius.Math.Vector(0, 1000, 0), 1),
        camera(Sirius.Math.Vector(0, 0, 0), Sirius.Math.Vector(0, 0, 0), J(), 200.0f),
        screen(800, 600),
        frame(0),
        frameIntervalMs(40u),
        minUs(MaxValue<int>()),
        maxUs(MinValue<int>())
    {
        SetBackgroundColor(Color.Black());
        SetSize(Size(screen.width, screen.height));
        UniquePtr<Cone> cone(new Cone(16, 100, 100, 0, 0, 0, Sirius.Math.Vector(0, 50, 0)));
        cone->TransformWorld();
        scene.AddObject(cone.Release());
        UniquePtr<Grid> grid(new Grid(8, 400, 0, cast<float>(PI() / 2), 0, Sirius.Math.Vector(0, -100, 0)));
        grid->TransformWorld();
        scene.AddObject(grid.Release());
        SetTimer(0u, frameIntervalMs);
    }
    protected override void OnSizeChanged(uint windowState)
    {
        base->OnSizeChanged(windowState);
        Size size = GetSize();
        screen.width = size.w;
        screen.height = size.h;
    }
    protected override void OnTimer(TimerEventArgs& args)
    {
        if (frame < 200)
        {
            TimePoint start = Now();
            float angle = 50 - 360.0f * frame / 100.0f;
            float t = cast<float>(DegToRad(angle));
            float radius = 256;
            float x = cast<float>(radius * Cos(t));
            float y = 100;
            float z = cast<float>(radius * Sin(t));
            camera.Move(Sirius.Math.Vector(x, y, z), camera.PointAt(), camera.Up(), camera.Distance());
            scene.View(camera, lightSource, screen);
            TimePoint end = Now();
            Duration duration = end - start;
            int us = cast<int>(duration.Microseconds());
            if (us > maxUs)
            {
                maxUs = us;
            }
            if (us < minUs)
            {
                minUs = us;
            }
            ++frame;
            Invalidate();
        }
        else
        {
            KillTimer(0u);
        }
    }
    protected override void OnPaint(PaintEventArgs& args)
    {
        BufferPtr bufferPtr = BufferPool.Instance().GetBufferPtr();
        scene.Draw(whitePen, System.Windows.Color.Black(), bufferPtr->BitmapGraphics());
        bufferPtr->Draw(args.graphics);
    }
    protected override void OnKeyPress(KeyPressEventArgs& args)
    {
        base->OnKeyPress(args);
        if (!args.handled)
        {
            if (args.keyChar == w't')
            {
                frame = 0;
                maxUs = MinValue<int>();
                minUs = MaxValue<int>();
                SetTimer(0u, frameIntervalMs);
            }
            else if (args.keyChar == w'm')
            {
                MessageBox.Show("max=" + ToString(maxUs) + ", min=" + ToString(minUs), "Map duration");
            }
        }
    }
    private System.Windows.Pen whitePen;
    private PointLightSource lightSource;
    private Camera camera;
    private Screen screen;
    private Scene scene;
    private int frame;
    private uint frameIntervalMs;
    private int minUs;
    private int maxUs;
}

int main()
{
    try
    {
        MainWindow mainWindow;
        return System.Windows.Application.Run(mainWindow);
    }
    catch (const Exception& ex)
    {
        System.Windows.MessageBox.Show(ex.Message());
        return 1;
    }
    return 0;
}



