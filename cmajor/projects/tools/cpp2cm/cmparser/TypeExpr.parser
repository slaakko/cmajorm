using System;
using cmast;

namespace cmparser
{
    grammar TypeExprGrammar
    {
        using BasicTypeGrammar.BasicType;
        using IdentifierGrammar.Identifier;
        using TemplateGrammar.TemplateId;
        using stdlib.spaces;
        skip spaces;
        
        TypeExpr(ParsingContext* ctx) : Node*
            ::= PrefixTypeExpr(ctx){ value = PrefixTypeExpr; }
            ;
            
        PrefixTypeExpr(ParsingContext* ctx) : Node*
            ::= keyword("const") PostfixTypeExpr(ctx):pf1{ value = new ConstNode(pf1); }
            |   PostfixTypeExpr(ctx):pf2{ value = pf2; }
            ;
            
        PostfixTypeExpr(ParsingContext* ctx, var UniquePtr<Node> node) : Node*
            ::= 
            (   PrimaryTypeExpr(ctx){ node.Reset(PrimaryTypeExpr); }
                (   '.' Identifier{ node.Reset(new DotNode(node.Release(), Identifier)); }
                |   '*'{ node.Reset(new PointerNode(node.Release())); }
                |   "&&"{ node.Reset(new RvalueRefNode(node.Release())); }
                |   '&'{ node.Reset(new LvalueRefNode(node.Release())); }
                )*
            )
            {
                value = node.Release();
            }
            ;
            
        PrimaryTypeExpr(ParsingContext* ctx) : Node*
            ::= BasicType{ value = BasicType; }
            |   TemplateId(ctx){ value = TemplateId; }
            |   Identifier{ value = Identifier; }
            ;
    }
}
