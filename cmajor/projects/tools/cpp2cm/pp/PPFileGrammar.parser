using System;
using cppparser;
using cppast;

namespace pp
{
    grammar PPFileGrammar
    {
        using stdlib.newline;
        using stdlib.identifier;
        using stdlib.sign;
        using cppparser.LiteralGrammar.CharacterLiteral;
        using cppparser.LiteralGrammar.StringLiteral;
        
        PPFile(PP* pp, ustring* result)
            ::= Group(pp, true, result)?
            ;
            
        Group(PP* pp, bool process, ustring* result)
            ::= 
            (   IfSection(pp, process, result)
            |   ControlLine(pp, process, result)
            |   TextLine(pp, process, result)
            )+
            ;
            
        IfSection(PP* pp, bool parentProcess, ustring* result, var bool processed, var bool processGroup)
            ::= IfGroup(pp, parentProcess, result){ processed = IfGroup; if (parentProcess && !processed) processGroup = true; else processGroup = false; } 
                (ElifGroup(pp, processGroup, result){ if (ElifGroup) processed = true; if (parentProcess && !processed) processGroup = true; else processGroup = false; })*
                ElseGroup(pp, processGroup, result)? 
                EndIfLine(pp, result)
            ;
            
        IfGroup(PP* pp, bool parentProcess, ustring* result, var bool process) : bool
            ::= (S? '#' S? keyword("if") S? ConstantExpression(parentProcess, pp){ process = parentProcess && ConstantExpression; } S? newline{ result->Append('\n'); } Group(pp, process, result):g1?){ value = process; }
            |   (S? '#' S? keyword("ifdef") S? identifier:i1{ process = parentProcess && pp->Defined(i1); } S? newline{ result->Append('\n'); } Group(pp, process, result):g2?){ value = process; }
            |   (S? '#' S? keyword("ifndef") S? identifier:i2{ process = parentProcess && !pp->Defined(i2); } S? newline{ result->Append('\n'); } Group(pp, process, result):g3?){ value = process; }
            ;

        ElifGroup(PP* pp, bool process, ustring* result) : bool
            ::= (S? '#' S? keyword("elif") S? ConstantExpression(process, pp){ process = ConstantExpression; } S? newline{ result->Append('\n'); } Group(pp, process, result)?){ value = process; }
            ;

        ElseGroup(PP* pp, bool process, ustring* result)
            ::= S? '#' S? keyword("else") S? newline{ result->Append('\n'); } Group(pp, process, result)?
            ;
                                    
        EndIfLine(PP* pp, ustring* result)
            ::= S? '#' S? keyword("endif") S? newline{ result->Append('\n'); }
            ;
            
        ControlLine(PP* pp, bool process, ustring* result)
            ::= IncludeDirective(pp, process, result)
            |   DefineFunctionMacro(pp, process, result)
            |   DefineObjectMacro(pp, process, result)
            |   UndefineMacro(pp, process, result)
            |   PragmaDirective(result)
            |   ErrorDirective(process, result)
            |   LineDirective(result)
            |   NullDirective(result)
            ;
            
        IncludeDirective(PP* pp, bool process, ustring* result, var ustring headerNameText)
            ::= (S? '#' S? keyword("include") S? NonemptyTokenLine(process, &headerNameText, pp, PPTokenFlags.acceptHeaderName) newline{ result->Append('\n'); })
            { 
                if (process) Preprocess(*pp, pp->ResolveHeaderFilePath(ToUtf8(headerNameText))); 
            }
            ;
            
        DefineFunctionMacro(PP* pp, bool process, ustring* result, var ustring replacementList)
            ::= (S? '#' S? keyword("define") S? token(identifier '(') MacroParams ')' S?
                PossiblyEmptyTokenLine(process, &replacementList, pp, cast<PPTokenFlags>(PPTokenFlags.acceptHeaderName | PPTokenFlags.acceptHash)) newline{ result->Append('\n'); })
            {
                if (process) pp->Define(identifier, MacroParams, replacementList, span); 
            }
            ;
            
        MacroParams : List<ustring>
            ::= (S? identifier:left{ value.Add(left); } (S? ',' S? identifier:right{ value.Add(right); })* S?)?
            ;
            
        DefineObjectMacro(PP* pp, bool process, ustring* result, var ustring replacementList)
            ::= (S? '#' S? keyword("define") S? identifier S? PossiblyEmptyTokenLine(process, &replacementList, pp, PPTokenFlags.acceptHeaderName) newline{ result->Append('\n'); })
            { 
                if (process) pp->Define(identifier, replacementList, false); 
            }
            ;
            
        UndefineMacro(PP* pp, bool process, ustring* result)
            ::= (S? '#' S? keyword("undef") S? identifier:macroName S? newline{ result->Append('\n'); }){ if (process) pp->Undefine(macroName); }
            ;
            
        PragmaDirective(ustring* result)
            ::= S? '#' S? keyword("pragma") S? token((anychar - newline)* newline){ result->Append('\n'); }
            ;
            
        ErrorDirective(bool process, ustring* result)
            ::= S? '#' S? keyword("error") S? token((anychar - newline)* newline)
            { 
                if (process) 
                {
                    result->Append(ustring(matchBegin, matchEnd)); 
                    ThrowPreprocessingException(ToUtf8(ustring(matchBegin, matchEnd)));
                }
                else 
                {
                    result->Append('\n'); 
                }
            }
            ;
            
        LineDirective(ustring* result)
            ::= S? '#' S? keyword("line") S? token((anychar - newline)* newline){ result->Append('\n'); }
            ;
            
        NullDirective(ustring* result)
            ::= (S? '#' S? newline){ result->Append('\n'); }
            ;
            
        ConstantExpression(bool process, PP* pp, var ustring constantExpressionText) : bool
            ::= NonemptyTokenLine(process, &constantExpressionText, pp, PPTokenFlags.none)
            { 
                if (process) value = EvaluateConstantExpression(constantExpressionText, pp, span.lineNumber); else value = false; 
            }
            ;
            
        TextLine(PP* pp, bool process, ustring* result, var ustring replacementText)
            ::= (PossiblyEmptyTokenLine(process, &replacementText, pp, PPTokenFlags.none) newline)
            { 
                if (process) result->Append(replacementText); 
                result->Append('\n');
            }
            ;
            
        PossiblyEmptyTokenLine(bool process, ustring* result, PP* pp, PPTokenFlags flags, var bool substituted, var int parenCount)
            ::= empty{ substituted = false; } (token(PPToken(process, result, pp, cast<PPTokenFlags>(flags | PPTokenFlags.acceptFunctionMacroInvocaction), &substituted, &parenCount)*))
                {
                    if (substituted) 
                    {
                        *result = ParsePossiblyEmptyTokenLine(process, *result, pp, flags); 
                    }
                }
            ;
            
        NonemptyTokenLine(bool process, ustring* result, PP* pp, PPTokenFlags flags, var bool substituted, var int parenCount) 
            ::= empty{ substituted = false; } (token(PPToken(process, result, pp, cast<PPTokenFlags>(flags | PPTokenFlags.acceptFunctionMacroInvocaction), &substituted, &parenCount)+))
                { 
                    if (substituted) 
                    {
                        *result = ParseNonemptyTokenLine(process, *result, pp, flags); 
                    }
                }
            ;
            
        PPToken(bool process, ustring* result, PP* pp, PPTokenFlags flags, bool* substituted, int* parenCount, var bool stringisize)
            ::= empty{ pass = (flags & PPTokenFlags.acceptHash) != PPTokenFlags.none; } ("##"{ if (process) result->Append('$'); } | '#'{ if (process) result->Append('@'); })
            |   empty{ pass = (flags & PPTokenFlags.acceptHeaderName) != PPTokenFlags.none; } HeaderName{ if (process) result->Append(HeaderName); }
            |   empty{ pass = (flags & PPTokenFlags.acceptFunctionMacroInvocaction) != PPTokenFlags.none; } FunctionMacroInvocation(process, pp, flags, result)
            |   empty{ pass = (flags & PPTokenFlags.macroArgumentReplacement) != PPTokenFlags.none; } 
                ('@'{ stringisize = true; } S?)? identifier:id1{ if (process) result->Append(pp->SubstituteMacroArgument(id1, stringisize)); }
            |   empty{ pass = (flags & PPTokenFlags.macroArgumentReplacement) == PPTokenFlags.none; } identifier:id2
            {
                 if (process) result->Append(pp->SubstituteObjectMacro(id2, *substituted, fileName, span.lineNumber)); 
            }
            |   PPNumber{ if (process) result->Append(PPNumber); }
            |   CharacterLiteral{ if (process) result->Append(ustring(matchBegin, matchEnd)); }
            |   StringLiteral{ if (process) result->Append(ustring(matchBegin, matchEnd)); }
            |   empty{ pass = (flags & PPTokenFlags.dontAcceptCommaOrRightParen) != PPTokenFlags.none; } 
            (   '('{ *parenCount = *parenCount + 1; if (process) result->Append('('); }
            |   ')'{ if (*parenCount == 0) pass = false; else { *parenCount = *parenCount - 1; if (process) result->Append(')'); } }
            |   ','{ if (*parenCount == 0) pass = false; else if (process) result->Append(','); }
            |   (anychar - ('#' | '(' | ')' | ',' | newline)){ if (process) result->Append(*matchBegin); }
            )
            |   empty{ pass = (flags & PPTokenFlags.dontAcceptCommaOrRightParen) == PPTokenFlags.none; } (anychar - ('#' | newline)){ if (process) result->Append(*matchBegin); }
            ;
            
        FunctionMacroInvocation(bool process, PP* pp, PPTokenFlags flags, ustring* result, var FunctionMacro* macro)
            ::= (token(identifier{ macro = pp->GetFunctionMacro(identifier); pass = macro != null; } '(') MacroArgumentList(process, pp, flags):args ')')
            {
                if (process) result->Append(DoFunctionMacroReplacement(macro, &args, pp, flags)); 
            }
            ;
            
        MacroArgumentList(bool process, PP* pp, PPTokenFlags flags) : List<ustring>
            ::= (MacroArgument(process, pp, flags):left{ value.Add(left); } (',' MacroArgument(process, pp, flags):right{ value.Add(right); })*)?
            ;
            
        MacroArgument(bool process, PP* pp, PPTokenFlags flags, var bool substituted, var int parenCount) : ustring
            ::= token((PPToken(process, &value, pp, cast<PPTokenFlags>(flags | PPTokenFlags.dontAcceptCommaOrRightParen), &substituted, &parenCount))+)
            ;
            
        PPTokenCatenation(PP* pp, PPTokenFlags flags, var ustring left, var ustring right, var bool substituted, var int parenCount) : ustring
            ::= (PPToken(true, &left, pp, cast<PPTokenFlags>(flags | PPTokenFlags.macroArgumentReplacement), &substituted, &parenCount):first
                (S? '$' S? PPToken(true, &right, pp, cast<PPTokenFlags>(flags | PPTokenFlags.macroArgumentReplacement), &substituted, &parenCount):next{ left.Append(right); right.Clear(); })*) 
            {
                value = left;
            }
            ;
            
        PPTokenSequence(PP* pp, PPTokenFlags flags) : ustring
            ::= PPTokenCatenation(pp, flags):left{ value = left; } (PPTokenCatenation(pp, flags):right{ value.Append(right); })*
            ;
            
        MacroReplacement(FunctionMacro* macro, List<ustring>* args, PP* pp, PPTokenFlags flags) : ustring
            ::= empty{ pp->PushFunctionMacro(macro, args); } ((PPTokenSequence(pp, flags){ value = PPTokenSequence; })?){ pp->PopFunctionMacro(); }
            ;
            
        PPNumber : ustring
            ::= token((digit | ('.' digit)) (digit | '.' | [eE] sign | [a-zA-Z_])*){ value = ustring(matchBegin, matchEnd); }
            ;
            
        HeaderName : ustring
            ::= token('<' [^>]+ '>'){ value = ustring(matchBegin, matchEnd); }
            |   token('"' [^"]+ '"'){ value = ustring(matchBegin, matchEnd); }
            ;
            
        S   ::= [ \t]+
            ;
    }
    
    grammar PPNonemptyTokenLineGrammar
    {
        using PPFileGrammar.NonemptyTokenLine;
        start NonemptyTokenLine;
    }
    
    grammar PPPossiblyEmptyTokenLineGrammar
    {
        using PPFileGrammar.PossiblyEmptyTokenLine;
        start PossiblyEmptyTokenLine;
    }
    
    grammar PPMacroReplacementGrammar
    {
        using PPFileGrammar.MacroReplacement;
        start MacroReplacement;
    }
}
