// =================================
// Copyright (c) 2020 Seppo Laakko
// Distributed under the MIT license
// =================================

using System.Lex;
using Cm.Ast;
using ParserFileTokens;

parser ParserFileParser
{
    uselexer ParserFileLexer;
    main;

    using ParIdentifierParser.QualifiedId;
    using ParIdentifierParser.Identifier;
    using ParTypeExprParser.TypeExpr;
    using ParExpressionParser.ExpressionList;
    using ParStatementParser.CompoundStatement;

    ParserFile(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.ParserFile> parserFile) : soulcm.scm2html.ParserFile*
        ::= (empty{ parserFile.Reset(new soulcm.scm2html.ParserFile(lexer.FileName())); } Declaration(parserFile.Get(), parsingContext):declaration*)
        {
            return parserFile.Release();
        }
        ;

    Declaration(soulcm.scm2html.ParserFile* parserFile, ParsingContext* parsingContext)
        ::= UsingDirective(parsingContext):usingDirective{ parserFile->AddUsingDirective(usingDirective); }
        |   ParserDeclaration(parsingContext):parserDeclaration{ parserFile->AddParser(parserDeclaration); }
        ;

    UsingDirective(ParsingContext* parsingContext) : Node*
        ::= UsingAliasDirective(parsingContext):usingAlias{ return usingAlias; }
        |   UsingNamespaceDirective(parsingContext):usingNs{ return usingNs; }
        ;

    UsingAliasDirective(ParsingContext* parsingContext, var Span s) : Node*
        ::= USING{ s = span; } Identifier(parsingContext):id ASSIGN QualifiedId(parsingContext):qid! SEMICOLON!
        {
            s.end = span.end;
            return new AliasNode(s, parsingContext->ModuleId(), id, qid);
        }
        ;

    UsingNamespaceDirective(ParsingContext* parsingContext, var Span s) : Node*
        ::= USING{ s = span; } QualifiedId(parsingContext):ns! SEMICOLON!
        {
            s.end = span.end;
            return new NamespaceImportNode(s, parsingContext->ModuleId(), ns);
        }
        ;

    ParserDeclaration(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.GrammarParser> grammarParser) : soulcm.scm2html.GrammarParser*
        ::= (PARSER ID{ grammarParser.Reset(new soulcm.scm2html.GrammarParser(lexer.GetMatch(span), u"")); } LBRACE! Statement(grammarParser.Get(), parsingContext):statement* RBRACE!)
        {
            return grammarParser.Release();
        }
        ;

    Statement(soulcm.scm2html.GrammarParser* grammarParser, ParsingContext* parsingContext)
        ::= MainStatement(grammarParser):mainStatement
        |   UsingStatement(grammarParser):usingStatement
        |   LexerStatement(grammarParser):lexerStatement
        |   RuleInfoStatement(grammarParser):ruleInfoStatement
        |   RuleStatement(grammarParser, parsingContext):ruleStatement
        ;

    MainStatement(soulcm.scm2html.GrammarParser* grammarParser)
        ::= MAIN SEMICOLON!{ grammarParser->SetMain(); }
        ;

    UsingStatement(soulcm.scm2html.GrammarParser* grammarParser)
        ::= USING UsingQualifiedId:parserId! SEMICOLON!{ grammarParser->AddUsing(parserId); }
        ;

    UsingQualifiedId(var Span s) : ustring
        ::= (ID{ s = span; } (DOT ID{ s.end = span.end; })*){ return lexer.GetMatch(s); }
        ;

    LexerStatement(soulcm.scm2html.GrammarParser* grammarParser, var ustring lexerName)
        ::= USELEXER ID!{ lexerName = lexer.GetMatch(span); } SEMICOLON!{ grammarParser->SetLexer(lexerName); }
        ;

    RuleInfoStatement(soulcm.scm2html.GrammarParser* grammarParser)
        ::= RULEINFO LBRACE! (RuleInfo(grammarParser):ruleInfo % COMMA)? RBRACE!
        ;

    RuleInfo(soulcm.scm2html.GrammarParser* grammarParser, var ustring ruleName, var ustring ruleInfo)
        ::= LPAREN ID!{ ruleName = lexer.GetMatch(span); } COMMA! STRINGLIT!{ ruleInfo = soulcm.scm2html.ParseStringLiteral(lexer.FileName(), lexer.GetToken(pos)); } RPAREN!
        {
            grammarParser->AddRuleInfo(ruleName, ruleInfo);
        }
        ;

    RuleStatement(soulcm.scm2html.GrammarParser* grammarParser, ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.RuleParser> rule)
        ::= ID{ ustring id = lexer.GetMatch(span); rule.Reset(new soulcm.scm2html.RuleParser(id)); } ParametersAndVariables(rule.Get(), parsingContext):paramsAndVars?
            ReturnType(parsingContext):returnType? PRODUCES RuleBody(parsingContext):definition! SEMICOLON!
        {
            rule->SetDefinition(definition);
            rule->SetReturnType(returnType);
            grammarParser->AddRule(rule.Release());
        }
        ;

    ParametersAndVariables(soulcm.scm2html.RuleParser* rule, ParsingContext* parsingContext)
        ::= LPAREN (ParamOrVariable(parsingContext):paramOrVariable{ rule->AddParamOrVariable(paramOrVariable); } % COMMA)? RPAREN!
        ;

    ParamOrVariable(ParsingContext* parsingContext) : soulcm.scm2html.Parameter*
        ::= VAR TypeExpr(parsingContext):varType Identifier(parsingContext):varName{ return new soulcm.scm2html.Variable(varType, varName); }
        |   TypeExpr(parsingContext):paramType Identifier(parsingContext):paramName{ return new soulcm.scm2html.Parameter(paramType, paramName); }
        ;

    ReturnType(ParsingContext* parsingContext) : Node*
        ::= COLON TypeExpr(parsingContext):type{ return type; }
        ;

    RuleBody(ParsingContext* parsingContext) : soulcm.scm2html.Parser*
        ::= Alternative(parsingContext):alt{ return alt; }
        ;

    Alternative(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::= (Sequence(parsingContext):left{ value.Reset(left); } (BITOR Sequence(parsingContext):right!{ value.Reset(new soulcm.scm2html.AlternativeParser(value.Release(), right)); })*)
        {
            return value.Release();
        }
        ;

    Sequence(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::= (Difference(parsingContext):left{ value.Reset(left); } (Difference(parsingContext):right{ value.Reset(new soulcm.scm2html.SequenceParser(value.Release(), right)); })*)
        {
            return value.Release();
        }
        ;

    Difference(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::= (List(parsingContext):left{ value.Reset(left); } (MINUS List(parsingContext):right!{ value.Reset(new soulcm.scm2html.DifferenceParser(value.Release(), right)); })*)
        {
            return value.Release();
        }
        ;

    List(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::= (Postfix(parsingContext):left{ value.Reset(left); } (REM Postfix(parsingContext):right!{ value.Reset(new soulcm.scm2html.ListParser(value.Release(), right)); })?)
        {
            return value.Release();
        }
        ;

    Postfix(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::=
        (   Primary(parsingContext):primary{ value.Reset(primary); }
            (   STAR{ value.Reset(new soulcm.scm2html.KleeneParser(value.Release())); }
            |   PLUS{ value.Reset(new soulcm.scm2html.PositiveParser(value.Release())); }
            |   QUEST{ value.Reset(new soulcm.scm2html.OptionalParser(value.Release())); }
            )?
        )
        {
            return value.Release();
        }
        ;

    Primary(ParsingContext* parsingContext, var UniquePtr<soulcm.scm2html.Parser> value) : soulcm.scm2html.Parser*
        ::=
        (
            (   RuleCall(parsingContext):ruleCall{ value.Reset(ruleCall); }
            |   Primitive:primitive{ value.Reset(primitive); }
            |   Grouping(parsingContext):grouping{ value.Reset(grouping); }
            )
            (
                EXCLAMATION{ value.Reset(new soulcm.scm2html.ExpectationParser(value.Release())); }
            )?
            (
                (
                    (CompoundStatement(parsingContext):successCode
                    (DIV CompoundStatement(parsingContext):failureCode)?){ value.Reset(new soulcm.scm2html.ActionParser(value.Release())); }
                )
            )?
        )
        {
            return value.Release();
        }
        ;

    RuleCall(ParsingContext* parsingContext, var ustring ruleName, var UniquePtr<InvokeNode> invoke, var long nonterminalPos) : soulcm.scm2html.Parser*
        ::= Nonterminal:nt{ ruleName = nt; nonterminalPos = pos; invoke.Reset(new InvokeNode(Span(), parsingContext->ModuleId())); }
            (LPAREN{ pass = NoWhiteSpaceBetweenTokens(lexer.GetToken(nonterminalPos), lexer.GetToken(pos)); }
            ExpressionList(parsingContext, invoke.Get()):args RPAREN)? COLON ID!
        {
            ustring name = lexer.GetMatch(span);
            soulcm.scm2html.NonterminalParser* nonterminalParser = new soulcm.scm2html.NonterminalParser(name, ruleName);
            nonterminalParser->SetArguments(invoke.Get());
            return nonterminalParser;
        }
        ;

    Nonterminal : ustring
        ::= ID{ return lexer.GetMatch(span); }
        ;

    Primitive : soulcm.scm2html.Parser*
        ::= EMPTY
        {
            return new soulcm.scm2html.EmptyParser();
        }
        |   ANY
        {
            return new soulcm.scm2html.AnyParser();
        }
        |   ID
        {
            ustring tokenName = lexer.GetMatch(span);
            return new soulcm.scm2html.TokenParser(tokenName);
        }
        |   CHARLIT
        {
            uchar charLit = soulcm.scm2html.ParseCharLiteral(lexer.FileName(), lexer.GetToken(pos));
            return new soulcm.scm2html.CharParser(charLit);
        }
        |   STRINGLIT
        {
            ustring strLit = soulcm.scm2html.ParseStringLiteral(lexer.FileName(), lexer.GetToken(pos));
            if (strLit.Length() > 0 && strLit[0] == '[' && strLit[strLit.Length() - 1] == ']')
            {
                soulcm.scm2html.CharSet charSet = soulcm.scm2html.ParseCharSet(lexer.FileName(), lexer.GetToken(pos), strLit);
                return new soulcm.scm2html.CharSetParser(charSet);
            }
            else
            {
                return new soulcm.scm2html.StringParser(strLit);
            }
        }
        ;

    Grouping(ParsingContext* parsingContext) : soulcm.scm2html.Parser*
        ::= (LPAREN Alternative(parsingContext):alt RPAREN!){ return new soulcm.scm2html.GroupingParser(alt); }
        ;
}
