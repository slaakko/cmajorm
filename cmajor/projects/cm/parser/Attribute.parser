// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Cm.Ast;
using Cm.Lexer;
using Cm.Parser;
using CmajorTokens;

parser AttributeParser
{
    uselexer CmajorLexer;

    Attributes(ParsingContext* ctx, var UniquePtr<Cm.Ast.AttributesNode> attributes) : Cm.Ast.AttributesNode*
        ::= LBRACKET{ attributes.Reset(new Cm.Ast.AttributesNode(span, ctx->ModuleId())); }
            (Attribute(ctx, attributes.Get()):attribute % COMMA)?
            RBRACKET!
        {
            return attributes.Release();
        }
        ;

    Attribute(ParsingContext* ctx, Cm.Ast.AttributesNode* attributes, var Span s, var ustring name, var ustring value)
        ::=
        (   empty{ value = u"true"; }
            ID{ s = span; name = lexer.GetMatch(span); }
            (   ASSIGN STRINGLIT
                {
                    s.end = span.end;
                    ParseStringLiteral(lexer.FileName(), lexer.GetToken(pos), lexer.strLit, lexer.strLitPrefix);
                    value = lexer.strLit;
                }
            )?
        )
        {
            attributes->AddAttribute(s, ctx->ModuleId(), name, value);
        }
        ;
}
