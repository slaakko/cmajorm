// =================================
// Copyright (c) 2020 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Cm.Ast;
using Cm.Lexer;
using Cm.Parser;
using CmajorTokens;

parser AttributeParser
{
    uselexer CmajorLexer;

    Attributes(var UniquePtr<Cm.Ast.AttributesNode> attributes) : Cm.Ast.AttributesNode*
        ::= LBRACKET{ attributes.Reset(new Cm.Ast.AttributesNode()); } 
            (Attribute(attributes.Get()):attribute % COMMA)?
            RBRACKET!
        {
            return attributes.Release();
        }
        ;
        
    Attribute(Cm.Ast.AttributesNode* attributes, var Span s, var ustring name, var ustring value)
        ::= 
        (   empty{ value = u"true"; }
            ID{ s = span; name = lexer.GetMatch(span); }
            (   ASSIGN STRINGLIT
                {
                    s.end = span.end;
                    ParseStringLiteral(lexer.FileName(), lexer.GetToken(pos), lexer.strLit, lexer.strLitPrefix);
                    value = lexer.strLit;
                }
            )?
        )
        {
            attributes->AddAttribute(s, name, value);
        }
        ;
}
