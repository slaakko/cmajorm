// =================================
// Copyright (c) 2020 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Cm.Ast;
using Cm.Lexer;
using Cm.Parser;
using CmajorTokens;

parser TypeExprParser
{
    uselexer CmajorLexer;
    
    using BasicTypeParser.BasicType;
    using ExpressionParser.Expression;
    using TemplateParser.TemplateId;

    TypeExpr(ParsingContext* ctx) : Node*
        ::= PrefixTypeExpr(ctx):prefixTypeExpr{ return prefixTypeExpr; }
        ;
        
    PrefixTypeExpr(ParsingContext* ctx, var Span s) : Node*
        ::= CONST{ s = span; } PostfixTypeExpr(ctx):constTypeExpr{ s.end = span.end; new ConstNode(s, constTypeExpr); }
        |   PostfixTypeExpr(ctx):postfixTypeExpr{ return postfixTypeExpr; }
        ;
        
    PostfixTypeExpr(ParsingContext* ctx, var UniquePtr<Node> typeExpr, var Span s) : Node*
        ::= 
        (   PrimaryTypeExpr(ctx):primaryTypeExpr{ s = span; typeExpr.Reset(primaryTypeExpr); }
            (   DOT ID!{ s.end = span.end; typeExpr.Reset(new DotNode(s, typeExpr.Release(), new IdentifierNode(span, lexer.GetToken(pos)))); }
            |   STAR{ s.end = span.end; typeExpr.Reset(new PointerNode(s, typeExpr.Release())); }
            |   AMPAMP{ s.end = span.end; typeExpr.Reset(new RValueRefNode(s, typeExpr.Release())); }
            |   AMP{ s.end = span.end; typeExpr.Reset(new LValueRefNode(s, typeExpr.Release())); }
            |   LBRACKET Expression(ctx):size? RBRACKET!{ s.end = span.end; typeExpr.Reset(new ArrayNode(s, typeExpr.Release(), size)); }
            )*
        )
        {
            return typeExpr.Release();
        }
        ;
        
    PrimaryTypeExpr(ParsingContext* ctx) : Node*
        ::= BasicType:basicType{ return basicType; }
        |   TemplateId(ctx):templateId{ return templateId; }
        |   ID{ return new IdentifierNode(span, lexer.GetToken(pos)); }
        ;
}
