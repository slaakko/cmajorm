// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Cm.Ast;
using Cm.NothrowParser;
using CmajorNothrowTokens;

parser NothrowTemplateParser
{
    uselexer CmajorNothrowLexer;
    nothrow;
    using NothrowIdentifierParser.QualifiedId;
    using NothrowIdentifierParser.Identifier;
    using NothrowTypeExprParser.TypeExpr;

    TemplateId(NothrowParsingContext* ctx, var UniquePtr<TemplateIdNode> templateId, var Span e) : Node*
        ::= empty{ ctx->BeginParsingTemplateId(); }
            (QualifiedId(ctx):primary{ templateId.Reset(new TemplateIdNode(span, ctx->ModuleId(), primary)); }
            LANGLE{ ++lexer.leftAngleCount; }
            ((TypeExpr(ctx):templateArg{ templateId->AddTemplateArgument(templateArg); } % COMMA) RANGLE{ e = span; }){ --lexer.leftAngleCount; } / { --lexer.leftAngleCount; })
        {
            templateId->SetSpanEnd(e.end);
            templateId->SetLexerFlags(lexer.Flags());
            ctx->EndParsingTemplateId();
            return templateId.Release();
        }
        /
        {
            ctx->EndParsingTemplateId();
        }
        ;

    TemplateParameter(NothrowParsingContext* ctx) : TemplateParameterNode*
        ::= Identifier(ctx):id ((ASSIGN TypeExpr(ctx):defaultTemplateArg)?)
        {
            TemplateParameterNode* node = new TemplateParameterNode(span, ctx->ModuleId(), id, defaultTemplateArg);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    TemplateParameterList(NothrowParsingContext* ctx, Node* owner)
        ::= LANGLE (TemplateParameter(ctx):tp{ owner->AddTemplateParameter(tp); } % COMMA) RANGLE!
        ;
}
