// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using Cm.Ast;
using Cm.NothrowParser;
using CmajorNothrowTokens;

parser NothrowConceptParser
{
    uselexer CmajorNothrowLexer;
    nothrow;

    using NothrowSpecifierParser.Specifiers;
    using NothrowIdentifierParser.Identifier;
    using NothrowIdentifierParser.QualifiedId;
    using NothrowParameterParser.ParameterList;
    using NothrowExpressionParser.Expression;
    using NothrowExpressionParser.InvokeExpr;
    using NothrowTypeExprParser.TypeExpr;
    using NothrowFunctionParser.FunctionGroupId;

    Concept(NothrowParsingContext* ctx, var UniquePtr<ConceptNode> conceptNode, var UniquePtr<WhereConstraintNode> constraint, var Span s, var Span beginBraceSpan, var Span endBraceSpan) : ConceptNode*
        ::= empty{ ctx->BeginParsingConcept(); }
        (   Specifiers:specifiers{ s = span; } CONCEPT Identifier(ctx):conceptName{ s.end = span.end; conceptNode.Reset(new ConceptNode(s, ctx->ModuleId(), specifiers, conceptName)); }
            LANGLE! (Identifier(ctx):typeParam{ conceptNode->AddTypeParameter(typeParam); } % COMMA) RANGLE!
            ((Refinement(ctx):refinement{ conceptNode->SetRefinement(refinement); })?
            (WhereConstraint(ctx):c{ s.end = span.end; constraint.Reset(c); constraint->SetHeaderConstraint(); conceptNode->AddConstraint(constraint.Release()); })?){ conceptNode->SetSpan(s); }
            LBRACE!{ beginBraceSpan = span; } ConceptBody(ctx, conceptNode.Get()):body RBRACE!{ endBraceSpan = span; }
        )
        {
            ctx->EndParsingConcept();
            conceptNode->SetBeginBraceSpan(beginBraceSpan);
            conceptNode->SetEndBraceSpan(endBraceSpan);
            conceptNode->SetLexerFlags(lexer.Flags());
            return conceptNode.Release();
        }
        /
        {
            ctx->EndParsingConcept();
        }
        ;

    Refinement(NothrowParsingContext* ctx, var UniquePtr<ConceptIdNode> conceptId ) : ConceptIdNode*
        ::=
        (   COLON QualifiedId(ctx):refinedConcept{ conceptId.Reset(new ConceptIdNode(span, ctx->ModuleId(), refinedConcept)); }
            LANGLE! (Identifier(ctx):typeParam{ conceptId->AddTypeParameter(typeParam); } % COMMA) RANGLE!
        )
        {
            conceptId->SetLexerFlags(lexer.Flags());
            return conceptId.Release();
        }
        ;

    ConceptBody(NothrowParsingContext* ctx, ConceptNode* conceptNode)
        ::= (ConceptBodyConstraint(ctx, conceptNode):bodyConstraint | Axiom(ctx, conceptNode):a)*
        ;

    ConceptBodyConstraint(NothrowParsingContext* ctx, ConceptNode* conceptNode)
        ::= TypeNameConstraint(ctx):typeNameConstraint{ conceptNode->AddConstraint(typeNameConstraint); }
        |   SignatureConstraint(ctx, conceptNode->TypeParameters()[0]):signatureConstraint{ conceptNode->AddConstraint(signatureConstraint); }
        |   EmbeddedConstraint(ctx):embeddedConstraint{ conceptNode->AddConstraint(embeddedConstraint); }
        ;

    TypeNameConstraint(NothrowParsingContext* ctx, var Span s) : ConstraintNode*
        ::= TYPENAME{ s = span; } TypeExpr(ctx):type SEMICOLON!
        {
            s.end = span.end;
            TypeNameConstraintNode* node = new TypeNameConstraintNode(s, ctx->ModuleId(), type);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    SignatureConstraint(NothrowParsingContext* ctx, IdentifierNode* firstTypeParameter) : ConstraintNode*
        ::= ConstructorConstraint(ctx, firstTypeParameter):constructorConstraint{ return constructorConstraint; }
        |   DestructorConstraint(ctx, firstTypeParameter):destructorConstraint{ return destructorConstraint; }
        |   MemberFunctionConstraint(ctx):memberFunctionConstraint{ return memberFunctionConstraint; }
        |   FunctionConstraint(ctx):functionConstraint{ return functionConstraint; }
        ;

    ConstructorConstraint(NothrowParsingContext* ctx, IdentifierNode* firstTypeParameter, var UniquePtr<ConstraintNode> ctorConstraint, var Span e) : ConstraintNode*
        ::=
        (   EXPLICIT? Identifier(ctx):id{ ustring className = lexer.GetMatch(span); pass = className == firstTypeParameter->Str(); if (pass) ctorConstraint.Reset(new ConstructorConstraintNode(span, ctx->ModuleId(), id)); }
            ParameterList(ctx, ctorConstraint.Get()):paramList SEMICOLON!{ e = span; }
        )
        {
            ctorConstraint->SetSpanEnd(e.end);
            ctorConstraint->SetLexerFlags(lexer.Flags());
            return ctorConstraint.Release();
        }
        ;

    DestructorConstraint(NothrowParsingContext* ctx, IdentifierNode* firstTypeParameter, var Span s) : ConstraintNode*
        ::=
        (   CPL{ s = span; } Identifier(ctx):id{ ustring className = lexer.GetMatch(span); pass = className == firstTypeParameter->Str(); } LPAREN! RPAREN! SEMICOLON!{ s.end = span.end; }
        )
        {
            DestructorConstraintNode* node = new DestructorConstraintNode(s, ctx->ModuleId(), id);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    MemberFunctionConstraint(NothrowParsingContext* ctx, var UniquePtr<ConstraintNode> memberFunctionConstraint) : ConstraintNode*
        ::= TypeExpr(ctx):returnType Identifier(ctx):typeParam DOT FunctionGroupId(ctx):functionGroupId
            {
                memberFunctionConstraint.Reset(new MemberFunctionConstraintNode(span, ctx->ModuleId(), returnType, typeParam, functionGroupId));
            }
            ParameterList(ctx, memberFunctionConstraint.Get()):paramList SEMICOLON!
            {
                memberFunctionConstraint->SetSpanEnd(span.end);
                memberFunctionConstraint->SetLexerFlags(lexer.Flags());
                return memberFunctionConstraint.Release();
            }
        ;

    FunctionConstraint(NothrowParsingContext* ctx, var UniquePtr<ConstraintNode> functionConstraint) : ConstraintNode*
        ::= TypeExpr(ctx):returnType FunctionGroupId(ctx):functionGroupId{ functionConstraint.Reset(new FunctionConstraintNode(span, ctx->ModuleId(), returnType, functionGroupId)); }
            ParameterList(ctx, functionConstraint.Get()):paramList SEMICOLON!
        {
            functionConstraint->SetSpanEnd(span.end);
            functionConstraint->SetLexerFlags(lexer.Flags());
            return functionConstraint.Release();
        }
        ;

    EmbeddedConstraint(NothrowParsingContext* ctx, var UniquePtr<WhereConstraintNode> constraint) : WhereConstraintNode*
        ::= WhereConstraint(ctx):c SEMICOLON!
        {
            constraint.Reset(c);
            constraint->SetSemicolon();
            constraint->SetLexerFlags(lexer.Flags());
            return constraint.Release();
        }
        ;

    WhereConstraint(NothrowParsingContext* ctx, var Span s) : WhereConstraintNode*
        ::= WHERE{ s = span; } ConstraintExpr(ctx):constraint
        {
            s.end = span.end;
            WhereConstraintNode* node = new WhereConstraintNode(s, ctx->ModuleId(), constraint);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    ConstraintExpr(NothrowParsingContext* ctx) : ConstraintNode*
        ::= DisjunctiveConstraintExpr(ctx):disjunctiveConstraint{ return disjunctiveConstraint; }
        ;

    DisjunctiveConstraintExpr(NothrowParsingContext* ctx, var Span s, var UniquePtr<ConstraintNode> constraint) : ConstraintNode*
        ::=
        (   ConjunctiveConstraintExpr(ctx):left{ constraint.Reset(left); s = span; }
            (   OR ConjunctiveConstraintExpr(ctx):right{ s.end = span.end; constraint.Reset(new DisjunctiveConstraintNode(s, ctx->ModuleId(), constraint.Release(), right)); })*
        )
        {
            constraint->SetLexerFlags(lexer.Flags());
            return constraint.Release();
        }
        ;

    ConjunctiveConstraintExpr(NothrowParsingContext* ctx, var Span s, var UniquePtr<ConstraintNode> constraint) : ConstraintNode*
        ::=
        (   PrimaryConstraintExpr(ctx):left{ constraint.Reset(left); s = span; }
            (   AND PrimaryConstraintExpr(ctx):right{ s.end = span.end; constraint.Reset(new ConjunctiveConstraintNode(s, ctx->ModuleId(), constraint.Release(), right)); })*
        )
        {
            constraint->SetLexerFlags(lexer.Flags());
            return constraint.Release();
        }
        ;

    PrimaryConstraintExpr(NothrowParsingContext* ctx, var Span s) : ConstraintNode*
        ::= LPAREN{ s = span; } ConstraintExpr(ctx):constraint RPAREN!
        {
            s.end = span.end;
            ParenthesizedConstraintNode* node = new ParenthesizedConstraintNode(s, ctx->ModuleId(), constraint);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        |   AtomicConstraintExpr(ctx):atomicConstraint{ return atomicConstraint; }
        ;

    AtomicConstraintExpr(NothrowParsingContext* ctx) : ConstraintNode*
        ::= PredicateConstraint(ctx):predicateConstraint{ return predicateConstraint; }
        |   IsConstraint(ctx):isConstraint{ return isConstraint; }
        |   MultiParamConstraint(ctx):multiParamConstraint{ return multiParamConstraint; }
        ;

    PredicateConstraint(NothrowParsingContext* ctx) : ConstraintNode*
        ::= InvokeExpr(ctx):invokeExpr
        {
            PredicateConstraintNode* node = new PredicateConstraintNode(span, ctx->ModuleId(), invokeExpr);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    IsConstraint(NothrowParsingContext* ctx, var Span s) : ConstraintNode*
        ::= TypeExpr(ctx):type{ s = span; } IS ConceptOrTypeName(ctx):conceptOrTypeName
        {
            s.end = span.end;
            IsConstraintNode* node = new IsConstraintNode(s, ctx->ModuleId(), type, conceptOrTypeName);
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;

    ConceptOrTypeName(NothrowParsingContext* ctx) : Node*
        ::= TypeExpr(ctx):conceptOrTypeName{ return conceptOrTypeName; }
        ;

    MultiParamConstraint(NothrowParsingContext* ctx, var UniquePtr<MultiParamConstraintNode> constraint) : ConstraintNode*
        ::= QualifiedId(ctx):id{ constraint.Reset(new MultiParamConstraintNode(span, ctx->ModuleId(), id)); }
            LANGLE (TypeExpr(ctx):typeExpr{ constraint->AddTypeExpr(typeExpr); } % COMMA) RANGLE!
        {
            constraint->SetSpanEnd(span.end);
            constraint->SetLexerFlags(lexer.Flags());
            return constraint.Release();
        }
        ;

    Axiom(NothrowParsingContext* ctx, ConceptNode* conceptNode, var UniquePtr<AxiomNode> axiomNode, var Span axiomSpan, var Span beginBraceSpan, var Span endBraceSpan)
        ::=
        (   AXIOM{ axiomSpan = span; } Identifier(ctx):id? empty{ axiomNode.Reset(new AxiomNode(axiomSpan, ctx->ModuleId(), id)); }
            ParameterList(ctx, axiomNode.Get()):paramList?
            LBRACE!{ beginBraceSpan = span; } AxiomBody(ctx, axiomNode.Get()):axiomBody RBRACE!{ endBraceSpan = span; }
        )
        {
            axiomNode->SetBeginBraceSpan(beginBraceSpan);
            axiomNode->SetEndBraceSpan(endBraceSpan);
            axiomNode->SetLexerFlags(lexer.Flags());
            conceptNode->AddAxiom(axiomNode.Release());
        }
        ;

    AxiomBody(NothrowParsingContext* ctx, AxiomNode* axiomNode)
        ::= AxiomStatement(ctx):stmt{ axiomNode->AddStatement(stmt); }
        ;

    AxiomStatement(NothrowParsingContext* ctx, var Span s) : AxiomStatementNode*
        ::= (Expression(ctx):expr{ s = span; } SEMICOLON!{ s.end = span.end; })
        {
            AxiomStatementNode* node = new AxiomStatementNode(s, ctx->ModuleId(), expr, lexer.GetMatch(s));
            node->SetLexerFlags(lexer.Flags());
            return node;
        }
        ;
}
