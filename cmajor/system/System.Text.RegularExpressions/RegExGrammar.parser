using System;

namespace System.Text.RegularExpressions
{
    grammar RegularExpressionGrammar
    {
        using stdlib.escape;

        RegularExpression(PtrNfaStateFactory* factory) : PtrNfa
            ::= AlternativeExpression(factory):alt{ value = alt; }
            ;

        AlternativeExpression(PtrNfaStateFactory* factory) : PtrNfa
            ::= SequenceExpression(factory):left{ value = left; } ('|' SequenceExpression(factory):right{ value = Alt(factory, value, right); })*
            ;

        SequenceExpression(PtrNfaStateFactory* factory) : PtrNfa
            ::= PostfixExpression(factory):left{ value = left; } (PostfixExpression(factory):right{ value = Cat(value, right); })*
            ;

        PostfixExpression(PtrNfaStateFactory* factory) : PtrNfa
            ::= PrimaryExpression(factory):primary{ value = primary; } 
                ('*'{ value = Kleene(factory, value); } | '+'{ value = Positive(factory, value); } | '?'{ value = Optional(value); })?
            ;

        PrimaryExpression(PtrNfaStateFactory* factory) : PtrNfa
            ::= Char:chr{ value = MakeNfa(factory, cast<char>(chr)); }
            |   '.'{ value = MakeNfa(factory, CharClass(string(eps), true)); }
            |   CharClass:cls{ value = MakeNfa(factory, cls); }
            |   ('(' AlternativeExpression(factory):alt ')'){ value = alt; }
            |   "\\d"{ value = MakeNfa(factory, CharClass(ToRanges(Class.digit), false)); }
            |   "\\D"{ value = MakeNfa(factory, CharClass(ToRanges(Class.digit), true)); }
            |   "\\s"{ value = MakeNfa(factory, CharClass(ToRanges(Class.space), false)); }
            |   "\\S"{ value = MakeNfa(factory, CharClass(ToRanges(Class.space), true)); }
            |   "\\w"{ value = MakeNfa(factory, CharClass(ToRanges(Class.word), false)); }
            |   "\\W"{ value = MakeNfa(factory, CharClass(ToRanges(Class.word), true)); }
            |   "\\l"{ value = MakeNfa(factory, CharClass(ToRanges(Class.lower), false)); }
            |   "\\L"{ value = MakeNfa(factory, CharClass(ToRanges(Class.lower), true)); }
            |   "\\u"{ value = MakeNfa(factory, CharClass(ToRanges(Class.upper), false)); }
            |   "\\U"{ value = MakeNfa(factory, CharClass(ToRanges(Class.upper), true)); }
            |   escape{ value = MakeNfa(factory, cast<char>(escape)); }
            ;

        Char : uchar
            ::= (anychar - [()\\\[*+?|.]){ value = *matchBegin; }
            ;

        CharClass(var bool inverse, var ustring s) : CharClass
            ::= (
                    '[' ('^'{ inverse = true; })?
                    (Class{ s.Append(ToUtf32(ToRanges(Class))); } | CharRange{ s.Append(CharRange); })*
                    ']'!
                )
                {
                    value = System.Text.RegularExpressions.CharClass(ToUtf8(s), inverse);
                }
                ;

        Class : Class
            ::= "[:alnum:]"{ value = Class.alnum; }
            |   "[:alpha:]"{ value = Class.alpha; }
            |   "[:blank:]"{ value = Class.blank; }
            |   "[:cntrl:]"{ value = Class.cntrl; }
            |   "[:digit:]"{ value = Class.digit; }
            |   "[:graph:]"{ value = Class.graph; }
            |   "[:lower:]"{ value = Class.lower; }
            |   "[:print:]"{ value = Class.print; }
            |   "[:punct:]"{ value = Class.punct; }
            |   "[:space:]"{ value = Class.space; }
            |   "[:upper:]"{ value = Class.upper; }
            |   "[:word:]"{ value = Class.word; }
            |   "[:w:]"{ value = Class.word; }
            |   "[:xdigit:]"{ value = Class.xdigit; }
            ;

        CharRange : ustring
            ::= CharClassChar:first
                ('-' CharClassChar:last
                    {
                        value.Append(first);
                        value.Append(u'-');
                        value.Append(last);
                    }
                |   empty{ value.Append(first); }
                )
                ;

        CharClassChar : uchar
            ::= [^\\\]]{ value = *matchBegin; }
            |   escape{ value = escape; }
            ;
    }
}
