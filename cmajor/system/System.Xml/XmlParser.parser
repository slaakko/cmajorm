// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

parser XmlParser
{
    uselexer TrivialLexer;
    farthest_error;
    state;
    main;

    Document(System.Xml.XmlProcessor* processor)
        ::= empty{ processor->StartDocument(); }
            (Prolog(processor):prolog Element(processor):element (Misc(processor):misc)*){ processor->EndDocument(); }
        ;

    Char : uchar
        ::= ("[\x9\xA\xD\x20-\xD7FF\xE000-\xFFFD\x10000-\x10FFFF]"){ Token token = lexer.GetToken(pos); return cast<uchar>(token.id); }
        ;

    S
        ::= "[\x20\x9\xD\xA]"+
        ;

    NameStartChar
        ::= "[:A-Z_a-z\xC0-\xD6\xD8-\xF6\xF8-\x2FF\x370-\x37D\x37F-\x1FFF\x200C-\x200D\x2070-\x218F\x2C00-\x2FEF\x3001-\xD7FF\xF900-\xFDCF\xFDF0-\xFFFD\x10000-\xEFFFF]"
        ;

    NameChar
        ::= NameStartChar:nameStartChar | "[-.0-9\xB7\x300-\x36F\x203F-\x2040]"
        ;

    Name(var Span s) : ustring
        ::= (NameStartChar:nameStartChar{ s = span; } (NameChar:nameChar{ s.end = span.end; })*)
        {
            return lexer.GetMatch(s);
        }
        ;

    Names
        ::= Name:name1 ('\x20' Name:name2)*
        ;

    Nmtoken
        ::= NameChar:nameChar+
        ;

    Nmtokens
        ::= Nmtoken:nmToken1 ('\x20' Nmtoken:nmToken2)*
        ;

    Prolog(System.Xml.XmlProcessor* processor)
        ::= XMLDecl(processor):xmlDecl? Misc(processor):m1* (DocTypeDecl(processor):docTypeDecl Misc(processor):m2*)?
        ;

    XMLDecl(System.Xml.XmlProcessor* processor)
        ::= "<?xml" VersionInfo(processor):versionInfo EncodingDecl(processor):encodingDecl? SDDecl(processor):sdDecl? S:s? "?>"
        ;

    VersionInfo(System.Xml.XmlProcessor* processor)
        ::= S:s "version" Eq:eq VersionNumber(processor):versionNumber
        ;

    VersionNum(System.Xml.XmlProcessor* processor, var Span s)
        ::= ('1'{ s = span; } '.' ("[0-9]"{ s.end = span.end; })+)
        {
            processor->Version(lexer.GetMatch(s));
        }
        ;

    VersionNumDQ(System.Xml.XmlProcessor* processor)
        ::= '"' VersionNum(processor):versionNum '"'
        ;

    VersionNumSQ(System.Xml.XmlProcessor* processor)
        ::= '\'' VersionNum(processor):versionNum '\''
        ;

    VersionNumber(System.Xml.XmlProcessor* processor)
        ::= VersionNumDQ(processor):versionNumDQ
        |   VersionNumSQ(processor):versionNumSQ
        ;

    EncodingDecl(System.Xml.XmlProcessor* processor)
        ::= S:s "encoding" Eq:eq EncName(processor):encName
        ;

    EncodingName(System.Xml.XmlProcessor* processor, var Span s)
        ::= ("[A-Za-z]"{ s = span; } ("[A-Za-z0-9._-]"{ s.end = span.end; })*)
        {
            processor->Encoding(lexer.GetMatch(s));
        }
        ;

    EncNameDQ(System.Xml.XmlProcessor* processor)
        ::= '"' EncodingName(processor):encodingName '"'
        ;

    EncNameSQ(System.Xml.XmlProcessor* processor)
        ::= '\'' EncodingName(processor):encodingName '\''
        ;

    EncName(System.Xml.XmlProcessor* processor)
        ::= EncNameDQ(processor):encNameDQ
        |   EncNameSQ(processor):encNameSQ
        ;

    SDDecl(System.Xml.XmlProcessor* processor)
        ::= S:s "standalone" Eq:eq YesNo:yn{ processor->Standalone(yn); }
        ;

    DocTypeDecl(System.Xml.XmlProcessor* processor)
        ::= "<!DOCTYPE" S:s1 Name:rootElementName (S:s2 ExternalID:extID)? S:s3? ('[' IntSubset(processor):intSubset ']' S:s4?)? '>'
        ;

    ExternalID
        ::= ("SYSTEM" S:s1 SystemLiteral:s2)
        |   ("PUBLIC" S:s3 PubidLiteral:p2 S:s4 SystemLiteral:s5)
        ;

    SystemLiteral
        ::= ('"' ("[^\"]"*) '"') | ('\'' ("[^']"*) '\'')
        ;

    PubidLiteral
        ::= '"' (PubidChar:p1*) '"' | '\'' ((PubidChar:p2 - '\'')*) '\''
        ;

    PubidChar
        ::= "[\x20\xD\xA]" | "[a-zA-Z0-9]" | "[-'()+,./:=?;!*#@$_%]"
        ;

    IntSubset(System.Xml.XmlProcessor* processor)
        ::= (MarkupDecl(processor):mdecl | DeclSep(processor):declsep)*
        ;

    MarkupDecl(System.Xml.XmlProcessor* processor)
        ::= ElementDecl(processor):elementDecl | AttlistDecl(processor):attlistDecl | EntityDecl(processor):entityDecl | NotationDecl(processor):notationDecl | PI(processor):pi | Comment(processor):comment
        ;

    DeclSep(System.Xml.XmlProcessor* processor)
        ::= PEReference(processor):peref | S:s
        ;

    ElementDecl(System.Xml.XmlProcessor* processor)
        ::= "<!ELEMENT" S:s1 Name:elementName S:s2 ContentSpec:contentSpec S:s3? '>'
        ;

    ContentSpec
        ::= "EMPTY" | "ANY" | Mixed:mixed | Children:children
        ;

    Children
        ::= (Choice:choice | Seq:seq) ('?' | '*' | '+')?
        ;

    CP
        ::= (Name:name | Choice:choice | Seq:seq) ('?' | '*' | '+')?
        ;

    Choice
        ::= '(' S:s1? CP:cp1 (S:s2? '|' S:s3? CP:cp2)+ S:s4? ')'
        ;

    Seq
        ::= '(' S:s1? CP:cp1 (S:s2? ',' S:s3? CP:cp2)* S:s4? ')'
        ;

    Mixed
        ::= '(' S:s1? "#PCDATA" (S:s2? '|' S:s3? Name:name)* S:s4? ")*"
        |   '(' S:s5? "#PCDATA" S:s6? ')'
        ;

    AttlistDecl(System.Xml.XmlProcessor* processor)
        ::= "<!ATTLIST" S:s1 Name:name AttDef(processor):attdef* S:s2? '>'
        ;

    AttDef(System.Xml.XmlProcessor* processor)
        ::= S:s Name:name S:s2 AttType:attType S:s3 DefaultDecl(processor):defaultDecl
        ;

    AttType
        ::= StringType:stringType | TokenizedType:tokenizedType | EnumeratedType:enumeratedType
        ;

    StringType
        ::= "CDATA"
        ;

    TokenizedType
        ::= "ID"
        |   "IDREF"
        |   "IDREFS"
        |   "ENTITY"
        |   "ENTITIES"
        |   "NMTOKEN"
        |   "NMTOKENS"
        ;

    EnumeratedType
        ::= NotationType:notationType | Enumeration:enumeration
        ;

    NotationType
        ::= "NOTATION" S:s1 '(' S:s2? Name:f (S:s3? '|' S:s4? Name:n)* S:s5? ')'
        ;

    Enumeration
        ::= '(' S:s1? Nmtoken:nmtoken (S:s2? '|' S:s3? Nmtoken:nmtoken2)* S:s4? ')'
        ;

    DefaultDecl(System.Xml.XmlProcessor* processor)
        ::= "#REQUIRED" | "#IMPLIED" | (("#FIXED" S:s)? AttValue(processor):attVAlue)
        ;

    EntityDecl(System.Xml.XmlProcessor* processor)
        ::= GEDecl(processor):gedecl | PEDecl(processor):pedecl
        ;

    GEDecl(System.Xml.XmlProcessor* processor)
        ::= "<!ENTITY" S:s0 Name:entityName S:s1 EntityDef(processor):entityValue S:s2? '>'
        ;

    PEDecl(System.Xml.XmlProcessor* processor)
        ::= "<!ENTITY" S:s0 '%' S:s1 Name:peName S:s2 PEDef(processor):peValue S:s3? '>'
        ;

    EntityDef(System.Xml.XmlProcessor* processor)
        ::= EntityValue(processor):entityValue | (ExternalID:extID NDataDecl:notation?)
        ;

    PEDef(System.Xml.XmlProcessor* processor)
        ::= EntityValue(processor):entityValue | ExternalID:extID
        ;

    EntityValue(System.Xml.XmlProcessor* processor)
        ::= '"'
            (   "[^%&\"]"
            |   PEReference(processor):pr1
            |   Reference(processor):ref1
            )* '"'
        |   '\''
            (   "[^%&']"
            |   PEReference(processor):pr2
            |   Reference(processor):ref2
            )* '\''
        ;

    NDataDecl
        ::= S:s1 "NDATA" S:s2 Name:name
        ;

    PEReference(System.Xml.XmlProcessor* processor)
        ::= '%' Name:name ';'
        ;

    NotationDecl(System.Xml.XmlProcessor* processor)
        ::= "<!NOTATION" S:s Name:name S:s2 (ExternalID:extID | PublicID:pubID) S:s3? '>'
        ;

    PublicID
        ::= "PUBLIC" S:s PubidLiteral:pl
        ;

    Element(System.Xml.XmlProcessor* processor, var ustring tagName)
        ::= '<' Name:name{ tagName = name; processor->BeginStartTag(tagName); } (S:s Attribute(processor):attribute)* S:s?
            (   "/>"{ processor->EndStartTag(span, lexer.FileName()); processor->EndTag(tagName, span, lexer.FileName()); }
            |   '>'{ processor->EndStartTag(span, lexer.FileName()); } Content(processor):content ETag(processor):etag
            )
        ;

    ETag(System.Xml.XmlProcessor* processor)
        ::= "</" Name:name{ processor->EndTag(name, span, lexer.FileName()); } S:s? '>'
        ;

    Content(System.Xml.XmlProcessor* processor)
        ::= CharData(processor):cd1? ((Element(processor):element | Reference(processor):reference | CDSect(processor):cdsect | PI(processor):pi | Comment(processor):comment) CharData(processor):cd2?)*
        ;

    CharDataChar : uchar
        ::= "[^<&]"{ Token token = lexer.GetToken(pos); return cast<uchar>(token.id); }
        ;

    CharData(System.Xml.XmlProcessor* processor, var ustring s)
        ::= ((CharDataChar:chr{ s.Append(chr); })* - ("[^<&]"* "]]>" "[^<&]"*)){ processor->Text(s); }
        ;

    CDSect(System.Xml.XmlProcessor* processor, var ustring s)
        ::= ("<![CDATA[" ((Char:chr - "]]>"){ s.Append(chr); })* "]]>")
        {
            processor->CDataSection(s);
        }
        ;

    Attribute(System.Xml.XmlProcessor* processor)
        ::= Name:attName Eq:eq AttValue(processor):attValue{ processor->AddAttribute(attName, attValue, span, lexer.FileName()); }
        ;

    AttValueDQ(System.Xml.XmlProcessor* processor) : ustring
        ::= '"'{ processor->BeginAttributeValue(); }
            (   "[^<&\"]"{ processor->AttValue().Append(lexer.GetMatch(span)); }
            |   Reference(processor):reference)* '"'
            {
                ustring value = processor->AttValue();
                processor->EndAttributeValue();
                return value;
            }
        ;

    AttValueSQ(System.Xml.XmlProcessor* processor) : ustring
        ::= '\''{ processor->BeginAttributeValue(); }
            (   "[^<&\']"{ processor->AttValue().Append(lexer.GetMatch(span)); }
            |   Reference(processor):reference)* '\''
            {
                ustring value = processor->AttValue();
                processor->EndAttributeValue();
                return value;
            }
        ;

    AttValue(System.Xml.XmlProcessor* processor) : ustring
        ::= AttValueDQ(processor):attValueDQ{ return attValueDQ; }
        |   AttValueSQ(processor):attValueSQ{ return attValueSQ; }
        ;

    EntityRef(System.Xml.XmlProcessor* processor)
        ::= ('&' Name:name ';'){ processor->EntityRef(name, span, lexer.FileName()); }
        ;

    DecCodePoint(var uint val) : uint
        ::= (("[0-9]"{ Token token = lexer.GetToken(pos); val = 10u * val + cast<uint>(token.id) - cast<uint>('0'); })+)
        {
            return val;
        }
        ;

    HexCodePoint(var uint val) : uint
        ::=
        (   ("[0-9a-fA-F]"
                {
                    Token token = lexer.GetToken(pos);
                    if (token.id >= cast<int>('0') && token.id <= cast<int>('9')) val = 16u * val + cast<uint>(token.id) - cast<uint>('0');
                    else if (token.id >= cast<int>('a') && token.id <= cast<int>('f')) val = 16u * val + 10u + cast<uint>(token.id) - cast<uint>('a');
                    else if (token.id >= cast<int>('A') && token.id <= cast<int>('F')) val = 16u * val + 10u + cast<uint>(token.id) - cast<uint>('A');
                }
            )+
        )
        {
            return val;
        }
        ;

    CharRef(System.Xml.XmlProcessor* processor)
        ::= ("&#" DecCodePoint:decCodePoint ';'){ processor->Text(ustring(cast<uchar>(decCodePoint))); }
        |   ("&#x" HexCodePoint:hexCodePoint ';'){ processor->Text(ustring(cast<uchar>(hexCodePoint))); }
        ;

    Reference(System.Xml.XmlProcessor* processor)
        ::= EntityRef(processor):entityRef
        |   CharRef(processor):charRef
        ;

    Misc(System.Xml.XmlProcessor* processor)
        ::= Comment(processor):comment | PI(processor):pi | S:s
        ;

    Comment(System.Xml.XmlProcessor* processor, var ustring s)
        ::=
        (   "<!--" ((Char:chr - '-'){ s.Append(chr); } | '-' (Char:chr - '-'){ s.Append('-').Append(chr); })* "-->"
        )
        {
            processor->Comment(s);
        }
        ;

    PI(System.Xml.XmlProcessor* processor, var ustring data)
        ::= ("<?" PITarget:target S:s ((Char:chr - "?>"){ data.Append(chr); })* "?>"){ processor->PI(target, data); }
        ;

    PITarget : ustring
        ::= (Name:name - Xml:xml){ return name; }
        ;

    Xml
        ::= "[xX]" "[mM]" "[lL]"
        ;

    Eq
        ::= S:s? '=' S:s?
        ;

    YesNo : bool
        ::= "\"yes\""{ return true; }
        |   "\"no\""{ return false; }
        ;
}
