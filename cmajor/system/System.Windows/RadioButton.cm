// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;

namespace System.Windows
{
    public nothrow ControlCreateParams& RadioButtonControlCreateParams(ControlCreateParams& controlCreateParams, ButtonStyle buttonStyle)
    {
        return controlCreateParams.SetWindowClassName("BUTTON").
            SetWindowStyle(cast<WindowStyle>(DefaultChildWindowStyle() | WindowStyle.WS_TABSTOP | buttonStyle));
    }

    public nothrow ControlCreateParams& RadioButtonControlCreateParams(ControlCreateParams& controlCreateParams)
    {
        return RadioButtonControlCreateParams(controlCreateParams, cast<ButtonStyle>(ButtonStyle.BS_RADIOBUTTON | ButtonStyle.BS_NOTIFY));
    }
    
    public class RadioButtonCreateParams
    {
        public nothrow RadioButtonCreateParams(ControlCreateParams& controlCreateParams_) :
            controlCreateParams(controlCreateParams_), autoSize(true)
        {
        }
        public nothrow RadioButtonCreateParams& Defaults()
        {
            return *this;
        }
        public nothrow RadioButtonCreateParams& SetAutoSize(bool autoSize_)
        {
            autoSize = autoSize_;
            return *this;
        }
        public ControlCreateParams& controlCreateParams;
        public bool autoSize;
    }
    
    public class RadioButton : ButtonBase
    {
        private enum Flags : sbyte
        {
            none = 0, checked = 1 << 0, autoSize = 1 << 1, autoSized = 1 << 2
        }

        public RadioButton(const Color& backgroundColor, const string& text, const Point& location, const Size& size, Dock dock, Anchors anchors,
            bool autoSize) : base("BUTTON", DefaultWindowClassStyle(),
            cast<WindowStyle>(DefaultChildWindowStyle() | WindowStyle.WS_TABSTOP | ButtonStyle.BS_RADIOBUTTON | ButtonStyle.BS_NOTIFY),
            DefaultExtendedWindowStyle(), backgroundColor, text, location, size, dock, anchors), flags(Flags.none)
        {
            if (autoSize)
            {
                SetAutoSizeFlag();
            }
            else
            {
                ResetAutoSizeFlag();
            }
        }
        public RadioButton(const string& text, const Point& location, const Size& size, Dock dock, Anchors anchors, bool autoSize) :
            this(DefaultControlBackgroundColor(), text, location, size, dock, anchors, autoSize)
        {
        }
        public RadioButton(RadioButtonCreateParams& createParams) : base(createParams.controlCreateParams), flags(Flags.none)
        {
            if (createParams.autoSize)
            {
                SetAutoSizeFlag();
            }
            else
            {
                ResetAutoSizeFlag();
            }
        }
        public bool Checked()
        {
            return GetCheckedFlag();
        }
        public void SetChecked(bool checked)
        {
            if (checked)
            {
                Control* parent = ParentControl();
                if (parent != null)
                {
                    if (parent is GroupBox*)
                    {
                        GroupBox* groupBox = cast<GroupBox*>(parent);
                        if (groupBox->CheckedRadioButton() != this)
                        {
                            groupBox->SetCheckedRadioButton(this);
                        }
                    }
                }
                SetCheckedFlag();
                if (Handle() != null)
                {
                    WinSendMessage(Handle(), BM_SETCHECK, BST_CHECKED, 0);
                }
                SetFocus();
            }
            else
            {
                Control* parent = ParentControl();
                if (parent != null)
                {
                    if (parent is GroupBox*)
                    {
                        GroupBox* groupBox = cast<GroupBox*>(parent);
                        if (groupBox->CheckedRadioButton() == this)
                        {
                            groupBox->ResetCheckedRadioButton();
                        }
                    }
                }
                ResetCheckedFlag();
                if (Handle() != null)
                {
                    WinSendMessage(Handle(), BM_SETCHECK, BST_UNCHECKED, 0);
                }
            }
        }
        protected override void OnCreated()
        {
            base->OnCreated();
            Graphics graphics = Graphics.FromWindowHandle(Handle());
            const FontHandle& fontHandle = GetFontHandle(graphics);
            if (!fontHandle.IsNull())
            {
                SendSetFontMessage(fontHandle);
            }
            if (GetCheckedFlag())
            {
                SetChecked(true);
            }
            else
            {
                SetChecked(false);
            }
            if (GetAutoSizeFlag())
            {
                DoAutoSize();
            }
        }
        protected override void OnClick()
        {
            base->OnClick();
            SetChecked(true);
        }
        protected override void OnTextChanged()
        {
            base->OnTextChanged();
            if (GetAutoSizeFlag())
            {
                ResetAutoSized();
                DoAutoSize();
            }
        }
        protected virtual void OnCheckedChanged()
        {
            checkedChangedEvent.Fire();
        }
        public nothrow Event<CheckedChangedEventHandler>& CheckedChangedEvent() const
        {
            return checkedChangedEvent;
        }
        private void RetrieveCheckedState()
        {
            long result = WinSendMessage(Handle(), BM_GETCHECK, 0u, 0);
            if (result == BST_CHECKED)
            {
                SetCheckedFlag();
            }
            else
            {
                ResetCheckedFlag();
            }
        }
        private void DoAutoSize()
        {
            if (AutoSized()) return;
            if (Handle() == null) return;
            Graphics graphics = Graphics.FromWindowHandle(Handle());
            const Font& font = GetFont();
            StringFormat stringFormat;
            RectF r = graphics.MeasureStringChecked(Text(), font, PointF(0, 0), stringFormat);
            Size buttonSize = GetButtonSize();
            Size borderSize = GetBorderSize();
            r.size.w = r.size.w + buttonSize.w + borderSize.w;
            r.size.h = Max(r.size.h, buttonSize.h + borderSize.h);
            SetSize(Size(cast<int>(r.size.w), cast<int>(r.size.h)));
            SetAutoSized();
        }
        private nothrow Size GetButtonSize()
        {
            int x = GetSystemMetrics(SystemMetricsId.SM_CXMENUCHECK);
            int y = GetSystemMetrics(SystemMetricsId.SM_CYMENUCHECK);
            return Size(x, y);
        }
        private nothrow Size GetBorderSize()
        {
            int x = GetSystemMetrics(SystemMetricsId.SM_CXBORDER);
            int y = GetSystemMetrics(SystemMetricsId.SM_CYBORDER);
            return Size(x, y);
        }
        private nothrow inline bool GetCheckedFlag() const
        {
            return (flags & Flags.checked) != Flags.none;
        }
        private void SetCheckedFlag()
        {
            if (!GetCheckedFlag())
            {
                flags = cast<Flags>(flags | Flags.checked);
                OnCheckedChanged();
            }
        }
        private void ResetCheckedFlag()
        {
            if (GetCheckedFlag())
            {
                flags = cast<Flags>(flags & ~Flags.checked);
                OnCheckedChanged();
            }
        }
        private nothrow inline bool GetAutoSizeFlag() const
        {
            return (flags & Flags.autoSize) != Flags.none;
        }
        private nothrow inline void SetAutoSizeFlag()
        {
            flags = cast<Flags>(flags | Flags.autoSize);
        }
        private nothrow inline void ResetAutoSizeFlag()
        {
            flags = cast<Flags>(flags & ~Flags.autoSize);
        }
        private nothrow inline bool AutoSized() const
        {
            return (flags & Flags.autoSized) != Flags.none;
        }
        private nothrow inline void SetAutoSized()
        {
            flags = cast<Flags>(flags | Flags.autoSized);
        }
        private nothrow inline void ResetAutoSized()
        {
            flags = cast<Flags>(flags & ~Flags.autoSized);
        }
        private Flags flags;
        private Event<CheckedChangedEventHandler> checkedChangedEvent;
    }
}
