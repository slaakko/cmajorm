// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using System.Collections;

class ParseResult
{
    public nothrow ParseResult() : command()
    {
    }
    public Command* GetCommand() 
    {
        return command.Get();
    }
    public nothrow void ResetCommand(Command* command_)
    {
        command.Reset(command_);
    }
    public nothrow Command* ReleaseCommand() 
    {
        return command.Release();
    }
    private UniquePtr<Command> command;
}

UniquePtr<Command> ParseCommand(const List<ustring>& commandWords)
{
    CommandLexer lexer(commandWords);
    ParseResult result;
    if (ParseCommand(lexer, result))
    {
        if (*lexer == END)
        {
            return UniquePtr<Command>(result.ReleaseCommand());
        }
        else
        {
            Console.Out() << "command syntax error:" << endl();
            lexer.PrintErrorLines();
        }
    }
    else
    {
        Console.Out() << "command syntax error:" << endl();
        lexer.PrintErrorLines();
    }
    return UniquePtr<Command>();
}

bool ParseCommand(CommandLexer& lexer, ParseResult& result)
{
    if (ParsePutCommand(lexer, result))
    {
        return true;
    }
	else if (ParseGetCommand(lexer, result))
	{
		return true;
	}
	else if (ParseBuildCommand(lexer, result))
	{
		return true;
	}
    else
    {
        return false;
    }
}

ustring ParsePath(CommandLexer& lexer)
{
    ustring path;
    bool first = true;
    while (*lexer != END)
    {
        if (first)
        {
            first = false;
        }
        else
        {
            path.Append(' ');
        }
        path.Append(lexer.GetWord());
        ++lexer;
    }
    return path;
}

bool ParsePutCommand(CommandLexer& lexer, ParseResult& result)
{
    int save = lexer.GetPos();
    if (*lexer == PUT)
    {
        ++lexer;
		bool force = false;
		if (*lexer == FORCE)
		{
			force = true;
			++lexer;
		}
        ustring path = ParsePath(lexer);
        if (!path.IsEmpty())
        {
            result.ResetCommand(new PutCommand(ToUtf8(path), force));
            return true;
        }
        else
        {
            lexer.SetPos(save);
            return false;
        }
    }
    else
    {
        lexer.SetPos(save);
        return false;
    }
}

bool ParseGetCommand(CommandLexer& lexer, ParseResult& result)
{
    int save = lexer.GetPos();
    if (*lexer == GET)
    {
        ++lexer;
		bool force = false;
		if (*lexer == FORCE)
		{
			force = true;
			++lexer;
		}
        ustring path = ParsePath(lexer);
        if (!path.IsEmpty())
        {
            result.ResetCommand(new GetCommand(ToUtf8(path), force));
            return true;
        }
        else
        {
            lexer.SetPos(save);
            return false;
        }
    }
    else
    {
        lexer.SetPos(save);
        return false;
    }
}

bool ParseBuildCommand(CommandLexer& lexer, ParseResult& result)
{
	int save = lexer.GetPos();
	if (*lexer == BUILD)
	{
		++lexer;
		Config config = Config.none;
		if (*lexer == DEBUG)
		{
			++lexer;
			config = cast<Config>(config | Config.debug);
		}
		if (*lexer == RELEASE)
		{
			++lexer;
			config = cast<Config>(config | Config.release);
		}
		string project;
		bool first = true;
		while (*lexer != END)
		{
			if (first)
			{
				first = false;
			}
			else
			{
				project.Append(' ');
			}
			project.Append(ToUtf8(lexer.GetWord()));
			++lexer;
		}
		if (!project.IsEmpty())
		{
			result.ResetCommand(new BuildCommand(project, config));
			return true;
		}
	}
	lexer.SetPos(save);
	return false;
}