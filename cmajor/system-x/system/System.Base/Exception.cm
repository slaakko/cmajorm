// =================================
// Copyright (c) 2022 Seppo Laakko
// Distributed under the MIT license
// =================================

namespace System
{
    public class Exception
    {
        public nothrow Exception(const string& message_) : message(message_), stackTrace()
        {
        }
        public default virtual ~Exception();
        public nothrow inline const string& Message() const
        {
            return message;
        }
        public nothrow const string& StackTrace() const
        {
            return stackTrace;
        }
        public void SetStackTrace(const string& stackTrace_)
        {
            stackTrace = stackTrace_;
        }
        public virtual string ToString() const
        {
            string s;
            s.Append(typename(*this)).Append(": ").Append(message).Append('\n').Append(stackTrace);
            return s;
        }
        private string message;
        private string stackTrace;
    }

    public void Throw(Exception* ex)
    {
        ex->SetStackTrace(GetStackTrace());
        if (do_throw(ex, typeid(ex)) == -1)
        {
            SystemError systemError = GetSystemError();
            Console.Error() << systemError.ToString() << endl();
        }
    }

    public class NullPointerException : Exception
    {
        public nothrow NullPointerException() : base("null pointer exception")
        {
        }
    }

    public class IndexOutOfBoundsException : Exception
    {
        public nothrow IndexOutOfBoundsException() : base("index out of bounds")
        {
        }
    }

    public class InvalidParameterException : Exception
    {
        public nothrow InvalidParameterException() : base("invalid parameter")
        {
        }
    }

    public class PreconditionViolationException : Exception
    {
        public nothrow PreconditionViolationException() : base("precondition violation")
        {
        }
    }

    public void ThrowNullPointerException()
    {
        throw NullPointerException();
    }

    public void ThrowIndexOutOfBoundsException()
    {
        throw IndexOutOfBoundsException();
    }

    public void ThrowInvalidParameterException()
    {
        throw InvalidParameterException();
    }

    public void ThrowPreconditionViolationException()
    {
        throw PreconditionViolationException();
    }
}
