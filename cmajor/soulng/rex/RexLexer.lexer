// =================================
// Copyright (c) 2021 Seppo Laakko
// Distributed under the MIT license
// =================================

#include <soulng/rex/RexApi.hpp>

classmap RexClassMap;

prefix "soulng/rex";

tokens RexTokens
{
    (LPAREN, "'('"), (RPAREN, "')'"), (LBRACKET, "'['"), (RBRACKET, "']'"),
    (ALT, "'|'"), (STAR, "'*'"), (PLUS, "'+'"), (QUEST, "'?'"), (DOT, "'.'"), (ESCAPE, "escape"), (INVERSE, "'^'"), (MINUS, "'-'"), (CHAR, "character")
}

expressions
{
    decdigit="[0-9]";
    decdigits="{decdigit}+";
    octaldigit="[0-7]";
    octaldigits="{octaldigit}+";
    hexdigit="[0-9a-fA-F]";
    hexdigits="{hexdigit}+";
    hex4 = "{hexdigit}{hexdigit}{hexdigit}{hexdigit}";
    hex8 = "{hex4}{hex4}";
    escape = "\\((x|X){hexdigit}+|(d|D){decdigit}+|{octaldigit}+|u{hex4}|U{hex8}|(a|b|f|n|r|t|v|.))";
}

lexer api(SOULNG_REX_API) RexLexer
{
    "\(" { return LPAREN; }
    "\)" { return RPAREN; }
    "\[" { return LBRACKET; }
    "\]" { return RBRACKET; }
    "\|" { return ALT; }
    "\*" { return STAR; }
    "\+" { return PLUS; }
    "\?" { return QUEST; }
    "\." { return DOT; }
    "{escape}" { return ESCAPE; }
    "^" { return INVERSE; }
    "-" { return MINUS; }
    "[^\x0\r\n{}\\()[\]|*+?.^-]" { return CHAR; }
}
