// =================================
// Copyright (c) 2019 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <cmajor/parsing/Scope.hpp>
[hpp]#include <cmajor/parsing/Parser.hpp>
[cpp]#include <cmajor/parsing/Composite.hpp>
[cpp]#include <cmajor/syntax/Primary.hpp>

namespace cmajor.syntax
{
    grammar Composite
    {
        using Primary.Primary;

        Alternative(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= Sequence(enclosingScope):left{ value = left; } ('|' Sequence(enclosingScope):right!{ value = new AlternativeParser(value, right); })*
                        ;

        Sequence(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= Difference(enclosingScope):left{ value = left; } (Difference(enclosingScope):right{ value = new SequenceParser(value, right); })*
                        ;

        Difference(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= ExclusiveOr(enclosingScope):left{ value = left; } ('-' ExclusiveOr(enclosingScope):right!{ value = new DifferenceParser(value, right); })*
                        ;

        ExclusiveOr(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= Intersection(enclosingScope):left{ value = left; } ('^' Intersection(enclosingScope):right!{ value = new ExclusiveOrParser(value, right); })*
                        ;

        Intersection(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= List(enclosingScope):left{ value = left; } ('&' List(enclosingScope):right{ value = new IntersectionParser(value, right); })*
                        ;

        List(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser* 
                        ::= Postfix(enclosingScope):left{ value = left; } ('%' Postfix(enclosingScope):right{ value = new ListParser(value, right); })?
                        ;

        Postfix(cmajor::parsing::Scope* enclosingScope): cmajor::parsing::Parser*
                        ::= Primary(enclosingScope){ value = Primary; }
                        (   '*'{ value = new KleeneStarParser(value); }
                        |   '+'{ value = new PositiveParser(value); }
                        |   '?'{ value = new OptionalParser(value); }
                        )?
                        ;
    }
}
